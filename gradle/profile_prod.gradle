dependencies {
    implementation "com.mysql:mysql-connector-j:${mysqlVersion}"
    testImplementation "org.testcontainers:testcontainers-mysql:${testcontainersVersion}"
}

ext {
    springProfiles = "prod" + springProfiles

    if (project.hasProperty("api-docs")) {
        springProfiles += ",api-docs"
    }
}

springBoot {
    buildInfo()
}

bootRun {
    args = ["--spring.profiles.active=${springProfiles}"]
}

tasks.register("webapp", NpmTask) {
    dependsOn npmInstall
    args = ["run", "webapp:prod"]
    environment = [APP_VERSION: project.version]
}

processResources {
    inputs.property("version", version)
    inputs.property("springProfiles", springProfiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
        filter {
            it.replace("#spring.profiles.active#", springProfiles)
        }
    }
}

processResources.dependsOn webapp
bootJar.dependsOn processResources
