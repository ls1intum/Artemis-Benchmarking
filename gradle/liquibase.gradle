configurations {
    liquibaseRuntime.extendsFrom sourceSets.main.compileClasspath
}

dependencies {
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"
    liquibaseRuntime "org.liquibase:liquibase-core:${liquibaseVersion}"
    // Dependency required to parse options. Refer to https://github.com/liquibase/liquibase-gradle-plugin/tree/Release_2.2.0#news.
    liquibaseRuntime "info.picocli:picocli:4.7.7"

    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate6:${liquibaseVersion}"
    liquibaseRuntime "com.mysql:mysql-connector-j:${mysqlVersion}"
}

project.ext.diffChangelogFile = "src/main/resources/config/liquibase/changelog/" + new Date().format("yyyyMMddHHmmss") + "_changelog.xml"
if (!project.hasProperty("runList")) {
    project.ext.runList = "main"
}

liquibase {
    activities {
        register("main") {
            driver = "com.mysql.cj.jdbc.Driver"
            url = "jdbc:mysql://localhost:3306/artemis-benchmarking"
            username = "root"
            changelogFile = "src/main/resources/config/liquibase/master.xml"
            logLevel = "debug"
            classpath = "src/main/resources/"
        }
        register("diffLog") {
            driver = "com.mysql.cj.jdbc.Driver"
            url = "jdbc:mysql://localhost:3306/artemis-benchmarking"
            username = "root"
            changelogFile = project.ext.diffChangelogFile
            referenceUrl = "hibernate:spring:de.tum.cit.aet.domain?dialect=org.hibernate.dialect.MySQL8Dialect&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
            logLevel = "debug"
            classpath = "${layout.buildDirectory.dir("classes/java/main")}"
        }
    }

    runList = project.ext.runList
}

liquibaseDiff.dependsOn compileJava
liquibaseDiffChangelog.dependsOn compileJava
